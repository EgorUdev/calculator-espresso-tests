apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.simplemobiletools.calculator"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 40
        versionName "5.5.2"
        setProperty("archivesBaseName", "calculator")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
	        applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'com.simplemobiletools:commons:5.34.25'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'net.objecthunter:exp4j:0.4.8'
    implementation 'junit:junit:4.13.2'

    implementation 'androidx.test:runner:1.4.0'
    implementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.test:rules:1.4.0'
    implementation 'androidx.test.ext:junit-ktx:1.1.3'
    implementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    implementation 'androidx.test:core:1.4.0'
    

}
